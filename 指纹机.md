# 指纹机
>[原文链接](http://www.instructables.com/id/DIY-Fingerprint-Scanning-Garage-Door-Opener/?ALLSTEPS) 作者:nodcah 翻译:Cosinx

标签（空格分隔）: arduino 

---
![](http://obook.qiniudn.com/FingerprintScanning0-1.jpg)

![](http://obook.qiniudn.com/FingerprintScanning0-2.jpg)

<embed src="http://player.youku.com/player.php/sid/XNzUzOTQ4NDg4/v.swf" allowFullScreen="true" quality="high" width="480" height="400" align="middle" allowScriptAccess="always" type="application/x-shockwave-flash"></embed>

我是一个没有车的人，不需要随身带车库钥匙，正因为这样，我已经被锁在门外很多次了，等别人的钥匙是非常痛苦的，所以我需要为此做些努力。

我解决这个问题的方案是加上一个指纹机的接口

当然，该模块不只局限于车库门，你可以创建不同类型的简单的电动锁，以满足你的需要。

##第一步：材料

![](http://obook.qiniudn.com/FingerprintScanning1-1.jpg)

![](http://obook.qiniudn.com/FingerprintScanning1-2.jpg)


>|指纹采集仪（带JST接头）|		[Sparkfun](https://www.sparkfun.com/products/11836)|

>|串口液晶套件（w/ATmega328）|		[Sparkfun](https://www.sparkfun.com/products/10097)|

>|ATtiny85芯片|		[Sparkfun](https://www.sparkfun.com/products/9378)|

>|PNP晶体管| 		[Sparkfun](https://www.sparkfun.com/products/522)|		[RadioShack](http://www.radioshack.com/product/index.jsp?productId=2062609)|

>|蜂鸣器	|	[Sparkfun](https://www.sparkfun.com/products/7950)|		[RadioShack](http://www.radioshack.com/product/index.jsp?productId=2062402)|

>|音频线	|	[RadioShack](http://www.radioshack.com/product/index.jsp?productId=2036278&retainProdsInSession=1&znt_campaign=Category_CMS&znt_source=CAT&znt_medium=RSCOM&znt_content=CT2032227)|

>|3D打印的盒子,详见第九步|

>|铜带|		[amazon](http://www.amazon.com/gp/product/B0086769IQ/ref=oh_details_o01_s00_i00?ie=UTF8&psc=1)|

>|5V稳压器|		[Sparkfun](https://www.sparkfun.com/products/107)|		[RadioShack](http://www.radioshack.com/product/index.jsp?productId=2062599)|

>|9V电池	|	[Sparkfun](https://www.sparkfun.com/products/10218)|		[RadioShack](http://www.radioshack.com/product/index.jsp?productId=4356385)|

>|9V电池座|		[Sparkfun](https://www.sparkfun.com/products/91)|		[RadioShack](http://www.radioshack.com/product/index.jsp?productId=2062218)|

>|单刀双掷开关	|	[Sparkfun](https://www.sparkfun.com/products/98)|		[RadioShack](http://www.radioshack.com/product/index.jsp?productId=2049719)|

以上列出了几乎所有的元器件（Sparkfun的清单）

###工具：

>烙铁/焊锡

>电工胶带

>导线/跳线

>打线刀/剥线钳

>PCB万用板

>电阻

>螺丝

>手钻

>LED（测试用）

>|5V USB 串口转换器	|	[Sparkfun](https://www.sparkfun.com/products/9716)|

>热胶枪

>3D打印机

>可选：IC芯片座（8芯用于ATtiny ，28 芯用于ATmega）

>可选：另外一个Arduino开发板 /10uF电容（详见第5步）

##第二步： 电路图

![](http://obook.qiniudn.com/FingerprintScanning2-1.jpg)

![](http://obook.qiniudn.com/FingerprintScanning2-2.jpg) 

![](http://obook.qiniudn.com/FingerprintScanning2-3.jpg)

![](http://obook.qiniudn.com/FingerprintScanning2-4.jpg)

![](http://obook.qiniudn.com/FingerprintScanning2-5.jpg)

![](http://obook.qiniudn.com/FingerprintScanning2-6.jpg)

通过Sparkfun买的液晶串行工具包带有一个ATMEGA328来控制LCD屏。ATmega除了控制LCD屏外，还有处理其他任务的能力，所以我们可以用它当作Arduino和指纹采集仪通信，发送ATtiny85的指令，控制LCD，用蜂鸣器发声。

为了防止模块持续运行太长时间，我添加了一个限位开关来检测什么情况下可以关闭机器。如果关机，电源断开（节省电池能量）

注意事项：指纹扫描仪在3.3V电平进行通信，所以建议使用一个分压器，从的ATmega电压降到3.2V。分压器由一个560Ω电阻和1K电阻组成，560Ω的电阻接FPS的2脚和D11，1K的电阻接D11和地（原文有误，对照电路后修改）。

LCD的串口引脚：

	D10:FPS脚1	（原文有误）
	
	D11:FPS脚2（通过分压电路）（原文有误）
	
	D12:ATtiny85
	
	D13：蜂鸣器
	
ATtiny85 引脚：

	5脚（代码中为0）：从ATmega输出入
	
	3脚（代码中为4）：接晶体管和黄色LED灯
	
	7脚（代码中为2）：接LED指示灯
	
##第三步：组装串行液晶套件

![](http://obook.qiniudn.com/FingerprintScanning3-1.jpg)

如题，这是个很不错的焊接工具包（从个人角度而言，我喜欢焊接）

如果需要的话，可以参考Sparkfun的手把手快速入门安装指南

你可以选择焊在28脚的芯片座上，这样你可以把芯片取出来，用于其他的项目

##第四步：组装电路板

![](http://obook.qiniudn.com/FingerprintScanning4-1.jpg)

![](http://obook.qiniudn.com/FingerprintScanning4-2.jpg)

![](http://obook.qiniudn.com/FingerprintScanning4-3.jpg)

![](http://obook.qiniudn.com/FingerprintScanning4-4.jpg)

板子的布局按自己喜欢的方式决定，但是，指纹扫描仪的连接线要朝同一个方向确保他们不会损坏（他们很容易断）

然后，我在板子的两面都涂上了热熔胶，用以稳固和绝缘。使用高温热熔胶是很好的（对于我来说，任何东西都不会燃烧、融化或毁坏）

至于主板，把该焊接的都焊好后，可以选择用热熔胶固定绝缘，稳压器可能会有点热，所以周围最好不要有热熔胶。你或许也不希望ATtiny芯片上有热熔胶，这样，你可以把它拿下来或者重新设计（take it out or reprogram it.）

##第五步：ATmega328 编程

![](http://obook.qiniudn.com/FingerprintScanning5-1.jpg)

就像在第二步中提到的，ATmega328有足够的处理能力，和足够的引脚来驱动LCD和其他的设备。为了充分利用这一点，你需要对芯片进行编程。

如果你有一个Arduino Uno 或者Duemilanove，你可以直接把上面的芯片用工具包里的芯片替换，或者，你可以使用Sparkfun的 FTDI Basic Breakout (5V)，并把它焊在板子上，（详细请看第三步图片）

当然，你需要把程序上传到Duemilanove 或 ATmega328

下面的例子用来判断板子是否能正常工作

	 LCD TestExample
	//LCDTestExample by Nodcah 
	//A simple sketch to make sure your Serial LCD Kit from Sparkfun
	//is working
	#include <LiquidCrystal.h>
	LiquidCrystal lcd(2,3,4,5,6,7,8);
	void setup() {
		pinMode(9, OUTPUT); //the backlight
		pinMode(13, OUTPUT); //the buzzer
		lcd.begin(16, 2); //16 chars wide, 2 tall
		digitalWrite(9, HIGH); //set the backlight to HIGH
  		lcd.print("  Hello world!  "); //use spaces to center the text
  		delay(2000);
	}

	void loop() { 
  		//buzzer turns on and off and its status is displayed on the LCD
  		lcd.clear();
  		lcd.print("  Buzzer is on  ");
  		tone(13, 262, 1000);
  		delay(1000);
  		lcd.clear();
  		lcd.print("  Buzzer is off ");
  		delay(1000);
  	}

##第六步：设置指纹扫描仪

![](http://obook.qiniudn.com/6-1.jpg)

为了和指纹扫描仪通信，我使用了Josh Hawley 写的Arduino库[这里](https://github.com/sparkfun/Fingerprint_Scanner-TTL/archive/master.zip)

我用这个LED闪烁的例子确保ATmega与扫描仪正常通信.

扫描仪有自己的存储单元存储指纹数据，在确认指纹机正常工作之后，上传这个例程，把你的指纹数据添加到数据库ID为#0的单元。只需打开串行控制台，按照提示操作。

	 Blink Example
	 
	/* 
	FPS_Enroll.ino - Library example for controlling the GT-511C3 Finger Print Scanner (FPS)
	Created by Josh Hawley, July 23rd 2013
	Licensed for non-commercial use, must include this license message
	basically, Feel free to hack away at it, but just give me credit for my work =)
	TLDR; Wil Wheaton's Law

	This simple sketch turns the LED on and off similar to the Arduino blink sketch.
	It is used to show that communications are working.
	*/

	#include "FPS_GT511C3.h"
	#include "SoftwareSerial.h"
	// Hardware setup - FPS connected to:
	//digital pin 10(arduino rx, fps tx)
	//digital pin 11(arduino tx - 560ohm resistor fps tx - 1000ohm resistor - ground)
	//this brings the 5v tx line down to about 3.2v so we dont fry our fps
	FPS_GT511C3 fps(10, 11);
	void setup(){
		Serial.begin(9600);
		fps.UseSerialDebug = true; // so you can see the messages in the serial debug screen
		fps.Open();
	}

	void loop(){
		// FPS Blink LED Test
		fps.SetLED(true); // turn on the LED inside the fps
		delay(1000);
		fps.SetLED(false);// turn off the LED inside the fps
		delay(1000);
	}

	 FPSEnroll Example
	/* 
 	FPS_Enroll.ino - Library example for controlling the GT-511C3 Finger Print Scanner (FPS)
 	Created by Josh Hawley, July 23rd 2013
 	Licensed for non-commercial use, must include this license message
 	basically, Feel free to hack away at it, but just give me credit for my work =)
 	TLDR; Wil Wheaton's Law
 	*/
 	
	#include "FPS_GT511C3.h"
	#include "SoftwareSerial.h"
	// Hardware setup - FPS connected to:
	//digital pin 10(arduino rx, fps tx)
	//digital pin 11(arduino tx - 560ohm resistor fps tx - 1000ohm resistor - ground)
	//this brings the 5v tx line down to about 3.2v so we dont fry our fps
	FPS_GT511C3 fps(10, 11);
	void setup(){
		Serial.begin(9600);
		delay(100);
		fps.Open();
		fps.SetLED(true);
		Enroll();
	}

	void Enroll(){
		// Enroll test
		// find open enroll id
		int enrollid = 0;
		fps.EnrollStart(enrollid);
		// enroll
		Serial.print("Press finger to Enroll #");
		Serial.println(enrollid);
		while(fps.IsPressFinger() == false) delay(100);
		bool bret = fps.CaptureFinger(true);
		int iret = 0;
		if (bret != false){
			Serial.println("Remove finger");
			fps.Enroll1(); 
			while(fps.IsPressFinger() == true) delay(100);
			Serial.println("Press same finger again");
			while(fps.IsPressFinger() == false) delay(100);
			bret = fps.CaptureFinger(true);
			if (bret != false){
				Serial.println("Remove finger");
				fps.Enroll2();
				while(fps.IsPressFinger() == true) delay(100);
				Serial.println("Press same finger yet again");
				while(fps.IsPressFinger() == false) delay(100);
				bret = fps.CaptureFinger(true);
				if (bret != false){
					Serial.println("Remove finger");
					iret = fps.Enroll3();
					if (iret == 0){
						Serial.println("Enrolling Successfull");
					}else{
						Serial.print("Enrolling Failed with error code:");
						Serial.println(iret);
					}
				}
				else Serial.println("Failed to capture third finger");
			}
			else Serial.println("Failed to capture second finger");
		}
		else Serial.println("Failed to capture first finger");
	}


	void loop(){
		delay(100000);
	}


##第七步：ATtiny85编程

![](http://obook.qiniudn.com/FingerprintScanning7-1.jpg)

![](http://obook.qiniudn.com/FingerprintScanning7-2.jpg)

ATtiny85是一款廉价的小型Arduion芯片（甚至是最好的东西之一），它可以和其它的Arduino进行编程，包括串口液晶套件里的ATmega328 
在这个项目中，它用来执行简单的任务：检查从ATmega收到的信号，如果信号是合法的，打开车库门。

为了实现编程，按上图所示连接电路，下载所需的文件，上传了这段代码后，Arduino的13脚（板子内部连接到led）要置为高电平，用来表示代码已经工作。

	fpsAttiny
	//fpsAttiny by Nodcah
	//Recieves a brief signal from the main module to close a relay

	void setup(){
		pinMode(2,OUTPUT); //indicator led through 10K resistor
		pinMode(4,OUTPUT); //trasistor pin that opens the garage
		pinMode(0,INPUT); //input 
		delay(500); //give things time to start up
		digitalWrite(2, HIGH); //indicator LED
	}

	void loop(){
		if(digitalRead(0)){ //simple pattern to trigger the transistor 
			delay(125);
			if(digitalRead(0)==false){ 
				delay(55); //the timings are off because the ATtiny's timer isn't perfect
				if(digitalRead(0)){
					delay(55);
					if(digitalRead(0)==false){
						delay(55);
						if(digitalRead(0)){
							delay(55);
							if(digitalRead(0)==false){
								digitalWrite(4, HIGH); //transistor "presses" the button
								delay(1000);
								digitalWrite(4,LOW);
								digitalWrite(2,LOW);
								delay(1000);
							}
						}
					}
				} 
			}
		}
	}


##第八步：最终代码

下面是我运用FPS和LCD库为这个项目写的Arduino代码。我已经尽力写了注释来描述代码的作用。如果你有什么问题，随便问我。

当这段代码上传以后，所有的器件都应该正常工作了，现在所要最的就是组装它

	 Code for ATmega238:

	/*FPSGarageDoorOpenner by Nodcah
 	*
 	*FPS_GT511C3 library created by Josh Hawley, July 23rd 2013
	*Licensed for non-commercial use, must include this license message
	*basically, Feel free to hack away at it, but just give me credit for my work =)
 	*TLDR; Wil Wheaton's Law
 	*
	*Opens a garage door if the scanned fingerprint is in
	*the FPS' database of prints.  
	*/
	
	#include "LiquidCrystal.h" //for the screen
	#include "FPS_GT511C3.h" //the fps (fingerprint scanner) library
	#include "SoftwareSerial.h" //used by fps library

	//Setting up the pins for the LCD and the fps
	LiquidCrystal lcd(2, 3, 4, 5, 6, 7, 8); //pinouts for LCD
	FPS_GT511C3 fps(10, 11); //RX, TX

	boolean isFinger = false; //true if the fps detects a finger on the scanner
	int timer = 0; //this is for when there is too long of a delay, it turns off

	//output pins
	const int buzzerPin = 13;
	const int backlightPin = 9;
	const int attinyPin = 12;

	void setup(){
		//set outputs
		pinMode(buzzerPin, OUTPUT);
		pinMode(backlightPin, OUTPUT);
		pinMode(attinyPin, OUTPUT);
	
		//for debugging
		//Serial.begin(9600);
		fps.UseSerialDebug = false; //set to true for fps debugging through serial

		//initializing the libraries
		lcd.begin(16,2);
		digitalWrite(backlightPin, HIGH); //the LCD backlight
		fps.Open();
		fps.SetLED(true); //the fps LED
		//boot up sound
		for(int i=0; i<30; i++){
			tone(buzzerPin, 50+10*i, 30);
			delay(30);
		}
		tone(buzzerPin, 350);

		//print starting message
		lcd.print("Put your finger "); //the command to print to the LCD
		lcd.setCursor(0, 1); //sets the cursor to the 0th column in the 1st row
		lcd.print(" on the scanner ");
		delay(150);
		noTone(buzzerPin); //stops the startup sound
	}
	
	void loop(){
		//scan and identify the finger when one is put on it
		waitForFinger();
		timer = 0; //resets timer for timeout
		lcd.clear(); //clears the screen and sets the cursor to 0,0
		fps.CaptureFinger(false); //captures the finger for identification
		int id = fps.Identify1_N(); //identifies print and stores the id
		if(id < 20){
			lcd.print(" Access granted "); //success message
			lcd.setCursor(0,1);

			//one line personalized messages
			//messages can't be more than 16 chars!!!'
			switch(id){
				case 0:
					lcd.print("    Hi self!    ");
					break;
				case 1:
					lcd.print("    Hey Bro!    ");
					break;
				case 2:
					lcd.print("Wat up homeslice");
					break; 
				case 3:
					lcd.print("    Hi Mom!     ");
					break;
				case 4:
					lcd.print("    Hey Dad!    ");
					break;
				case 5:
					lcd.print("   Hi Auntie!   ");
					break;
				case 6:
					lcd.print("  Hi Grandma!   ");
					break;
				case 7:
					lcd.print("   Hi Zeide!    ");
					break;
			}
			tone(buzzerPin, 262, 1000);
			delay(1500);

			//sends simple pattern to attiny inside garage  (for security)
			digitalWrite(attinyPin, HIGH);
			delay(100);
			digitalWrite(attinyPin, LOW);
			delay(50);
			digitalWrite(attinyPin, HIGH);
			delay(50);
			digitalWrite(attinyPin, LOW);
			delay(50);
			digitalWrite(attinyPin, HIGH);
			delay(50);
			digitalWrite(attinyPin, LOW);
			delay(1000);
			
			lcd.clear();
			lcd.print("Don't forget to ");
			lcd.setCursor(0,1);
			lcd.print("  shut me off!  ");
			
			waitForFinger(); //tap to continue to enroll
			
			while(true){
				//save a new fingerprint
				//prints message to lcd
				lcd.clear();
				lcd.print(" So you want to ");
				lcd.print("scan a new one? ");
				delay(2000);

				//Copied and slightly modified from the enroll example:
				int enrollid = 1;
				
				//choosing which id to overwrite/create
				//release your finger when you want to write to the id printed on the screen

				waitForFinger(); //waits for the fps to be pressed
				if(fps.IsPressFinger() == true){
					lcd.clear();
					lcd.print("       1?       ");
					delay(1000);
					if(fps.IsPressFinger() == true){
						lcd.clear();
						lcd.print("       2?       ");
						delay(1000);
						if(fps.IsPressFinger() == true){
							lcd.clear();
							lcd.print("       3?       ");
							if(fps.IsPressFinger() == true){
								lcd.clear();
								lcd.print("       4?       ");
								delay(1000);
								if(fps.IsPressFinger() == true){
									lcd.clear();
									lcd.print("       5?       ");
									delay(1000);
									if(fps.IsPressFinger() == true){
										lcd.clear();
										lcd.print("       6?       ");
										delay(1000);
										if(fps.IsPressFinger() == true){
											lcd.clear();
											lcd.print("       7?       ");
											delay(1000);
											if(fps.IsPressFinger() == true){
												lcd.clear();
												lcd.print("       8?       ");
												delay(1000);
												if(fps.IsPressFinger() == true){
													lcd.clear();
													lcd.print("       9?       ");
													delay(1000);
													if(fps.IsPressFinger() == true){
														lcd.clear();
														lcd.print("      10?       ");
														delay(1000);
														enrollid = 10; //can be expanded to up to 20
													}
													else enrollid = 9;
												}
												else enrollid = 8;
											}
											else enrollid = 7;
										}
										else enrollid = 6;
									}
									else enrollid = 5;
								}
								else enrollid = 4;
							}
							else enrollid = 3;
						}else enrollid = 2;
					}
					else enrollid = 1;
				}

				//warning if there is already data in this id slot
				if(fps.CheckEnrolled(enrollid)){ 
					lcd.clear();
					lcd.print(" Warning! ID #");
					lcd.print(enrollid);
					lcd.setCursor(0,1);
					lcd.print(" has data. OK?  ");

					waitForFinger(); //waits for the fps to be pressed

					fps.DeleteID(enrollid); //delete data
					delay(100);
				}

				//Enroll
				fps.EnrollStart(enrollid);
				lcd.clear(); 
				lcd.print("Place finger to ");
				lcd.setCursor(0,1);
				lcd.print("enroll #");
				lcd.print(enrollid); //prints id that is being enrolled
				waitForFinger(); //waits for the fps to be pressed

				//captures the finger and saves to memory three times for accurate data
				bool bret = fps.CaptureFinger(true); //high quality pic for enrollment
				int iret = 0; //error stuff

				if (bret != false){ //first enroll
					lcd.clear();
					lcd.print(" Remove finger  ");
					fps.Enroll1();
					while(fps.IsPressFinger() == true) delay(100); //waits until no finger
					lcd.clear();
					lcd.print("  Press again   ");
					waitForFinger(); //waits for the fps to be pressed
					bret = fps.CaptureFinger(true);
	
					if (bret != false){ //second enroll
						lcd.clear();
						lcd.print(" Remove finger  ");
						fps.Enroll2();
						while(fps.IsPressFinger() == true) delay(100);
						lcd.clear();
						lcd.print("Press yet again ");
						waitForFinger(); 
						bret = fps.CaptureFinger(true);

						if (bret != false){ //third enroll
							iret = fps.Enroll3();
							if (iret == 0){ //checks to see if there are any errors
								lcd.clear();
								lcd.print("    Success!    ");
								delay(2000);
								beep(); //shuts arduino off
							}
							else
							{ 
							//if the enrollment fails in any way
							lcd.clear();
							lcd.print("Fail. Try again ");
							delay(1000);
							}
						}
						lcd.clear();
						lcd.print("   Failed 3rd   "); //error on 3rd
					}
					lcd.clear();
					lcd.print("   Failed 2nd   "); //error on 2nd
					delay(1000);
				}
				lcd.clear();
				lcd.print("   Failed 1st   "); //error on 1st
				delay(1000);
			}
		}
		else{
			lcd.print("Fingerprint is"); //if print isn't recognized
			lcd.setCursor(0,1);
			lcd.print("   unverified   ");
			delay(2000);
			lcd.clear();
			lcd.print("Please try again");
			lcd.setCursor(0,1);
			lcd.print("Use your pointer"); //I scanned everyone's pointer finger
			delay(500);
		}
		delay(250);
	}


	void beep(){ 
		//beeps in hopes of someone closing the case
		lcd.clear();
		lcd.print("Please close the");
		lcd.setCursor(0,1);
		lcd.print("     case!      ");
		for(int i=0;i<8;i++){
			tone(buzzerPin, 262, 500);
			delay(1000);
		}
		delay(5000); //wait for someone to close the case
		//if no one does, shut everything off
		lcd.clear();
		digitalWrite(backlightPin, LOW);
		fps.SetLED(LOW);
		while(true) delay(10000);
	}

	void waitForFinger(){
		timer = 0; //resets the timer everytime this function starts
		while(fps.IsPressFinger() == false){ //timeout
			timer++;
			delay(100); 
			if (timer>=80){
				timer = 0; //reset timer
				break;
			}
		}
		if(fps.IsPressFinger() == false)beep(); 
		timer = 0; //resets the timer everytime this function ends
	}
	
##第九步：3D打印盒

![](http://obook.qiniudn.com/FingerprintScanning9-1.jpg)

![](http://obook.qiniudn.com/FingerprintScanning9-2.jpg)

![](http://obook.qiniudn.com/FingerprintScanning9-3.jpg)

![](http://obook.qiniudn.com/FingerprintScanning9-4.jpg)

![](http://obook.qiniudn.com/FingerprintScanning9-5.jpg)

为了要打开模块，触发限位开关，这个盒子需要能向上滑动。如图所示，限位开关连接到公共端（C）和常闭端（NC）

然后用热熔胶粘好每一个器件，限位开关需要稍微倾斜便于按下；

>[3D盒子](http://obook.qiniudn.com/FingerprintScanningMainCase.stl)

>[3D盖子](http://obook.qiniudn.com/FingerprintScanningCover.stl)

##第十步：车库准备

![](http://obook.qiniudn.com/FingerprintScanning10-1.gif)

![](http://obook.qiniudn.com/FingerprintScanning10-2.jpg)

![](http://obook.qiniudn.com/FingerprintScanning10-3.jpg)

![](http://obook.qiniudn.com/FingerprintScanning10-4.jpg)

![](http://obook.qiniudn.com/FingerprintScanning10-5.jpg)

![](http://obook.qiniudn.com/FingerprintScanning10-6.jpg)

为了打开车库门，我把ATtiny85连接到了开门按钮，我用一个NPN的晶体管连接代替了直接用导线连接。

连接线需要先测试，并剪到合适的尺寸，稍微留出一点裕量。然后，比较困难的部分是：把开关焊接到指纹机模块（如GIF图所示），之后用绝缘胶包裹线头

要使车库内的ATtiny得到车库外的ATmega的信号，三根线（电源，地，信号线）需要穿过墙，在我的车库里，有一块木头，我钻通了眼（如图所示）

最后，拧上盒子，启动它

##第十一步：测试

<embed src="http://player.youku.com/player.php/sid/XNzUzOTQ4NDg4/v.swf" allowFullScreen="true" quality="high" width="480" height="400" align="middle" allowScriptAccess="always" type="application/x-shockwave-flash"></embed>

![](http://obook.qiniudn.com/FingerprintScanning11-1.jpg)

![](http://obook.qiniudn.com/FingerprintScanning11-2.jpg)

现在是最有趣的部分！使用该模块内置的登记功能，使家人/朋友可以打开车库。然后，为每个人创建个性化的信息。看视频了解详细功能

##第十二步：让电池为它供电

![](http://obook.qiniudn.com/FingerprintScanning12-1.jpg)

<embed src="http://player.youku.com/player.php/sid/XNzYwMjEyODUy/v.swf" allowFullScreen="true" quality="high" width="480" height="400" align="middle" allowScriptAccess="always" type="application/x-shockwave-flash"></embed>

![](http://obook.qiniudn.com/FingerprintScanning12-2.jpg)

![](http://obook.qiniudn.com/FingerprintScanning12-3.jpg)

如果用电池供电，就可以把指纹机和液晶显示屏放在一个盒子里。我从车库上取下模块（暂时的），再加上一个伺服电机，就可以通过指纹机锁住一个盒子

注意：我发现9V的电池不能满足模块和伺服电机的电流，所以我用6节5号电池代替。我的设计只是为了演示，为了安全起见，建议用更完善的设计。
